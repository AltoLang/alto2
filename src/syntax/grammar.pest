// since the pratt parser ignores the top-most
// token, we'll need a module rule and a root rule
// since we need the root rule to contains a list
// of subtokens.

module = { (statement)* }

WHITESPACE = _{ " " | "\t" }
NEWLINE = _{ "\n" | "\n\r" }
number_token = @{ ("+" | "-")? ~ ASCII_DIGIT+ }
letters = _{ (ASCII_ALPHA | "_")+ }

assign_operator = { "->" }

operation = _{ addition | subtraction | multiplication | division }
    addition = { "+" }
    subtraction = { "-" }
    multiplication = { "*" }
    division = { "/" }

keyword = _{ let_keyword | if_keyword }
    let_keyword = { "let" }
    if_keyword = { "if" }

string_token = { "\"" ~ letters ~ "\"" | "'" ~ letters ~ "'" }
identifier_token = @{ !keyword ~ letters }

statement = _{ code_block_statement | declaration_statement | expression_statement }
    code_block_statement = { "{" ~ (statement)* ~ "}" }
    declaration_statement = { let_keyword ~ identifier_token ~ assign_operator ~ expression}
    expression_statement = { expression }

atom = _{
    assignment_expression |
    function_definition_expression |
    call_expression |
    number_token |
    string_token |
    reference_expression |
    "(" ~ expression ~ ")"
}

function_definition_expression = { "func" ~ identifier_token ~ parameters_expression ~ code_block_statement }
parameters_expression = { "(" ~ ( (func_parameter ~ ",")* ~ func_parameter+ )? ~ ")"}
func_parameter = { identifier_token ~ ":" ~ identifier_token }

assignment_expression = { identifier_token ~ assign_operator ~ expression }
reference_expression = { identifier_token }

call_expression = { identifier_token ~ function_args }
function_args = { "(" ~ ((func_argument ~ ",")* ~ func_argument +)? ~ ")"}
func_argument = _{ expression }

expression = { atom ~ (operation ~ atom)* }